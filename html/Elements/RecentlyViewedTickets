<&|/Widgets/TitleBox, title => loc("Tickets I viewed recently"), &>

<table class="ticket-list collection-as-table">
<&/Elements/CollectionAsTable/Header,
    Class        => 'RT__Ticket',
    Format       => \@Format,
    FormatString => $Format,
    maxitems     => $maxitems,
    AllowSorting => 0,
&>

% my ($i, $column_map) = (0, {});
% for my $id ( @$ids ) {
%   my $ticket = RT::Ticket->new( $session{CurrentUser} );
%   $ticket->Load($id);
%   next unless $ticket->id;
%   $m->flush_buffer unless ++$i % 10;
<&/Elements/CollectionAsTable/Row,
    i         => $i,
    Format    => \@Format,
    record    => $ticket,
    maxitems  => $maxitems,
    ColumnMap => $column_map,
    Class     => 'RT__Ticket',
&>
% }

</table>
</&>

<%INIT>
my $ids = $session{CurrentUser}->UserObj->Preferences('RecentlyViewedTickets', [] );
my $limit = RT->Config->Get('RecentlyViewedTicketsLimit', $session{CurrentUser}) || 10;
if ( @$ids > $limit ) {
    @$ids = @$ids[0 .. $limit-1];
}

my $Format =
     RT->Config->Get( 'RecentlyViewedTicketsFormat', $session{CurrentUser} )
  || RT->Config->Get( 'DefaultSearchResultFormat', $session{CurrentUser} );
$Format = $m->comp( '/Elements/ScrubHTML', Content => $Format );
my @Format =
  $m->comp( '/Elements/CollectionAsTable/ParseFormat', Format => $Format );

my ( $maxitems, $item ) = ( 0, 0 );
for my $col (@Format) {
    $item++;
    if ( $col->{title} && ( $col->{title} eq 'NEWLINE' ) ) {
        $item = 0;
    }
    else {
        $maxitems = $item if $item > $maxitems;
    }
}

</%INIT>

